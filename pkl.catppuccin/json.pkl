module pkl.catppuccin.json
import "Catppuccin.pkl" as ctp
import "pkl:json"

local function dynToPaletteColor(_obj: Dynamic): ctp.Color = new {
  name = _obj.name
  order = _obj.order
  hex = _obj.hex
  rgb = new {..._obj.rgb}
  hsl = new {..._obj.hsl}
  accent = _obj.accent
}

local function dynToTheme(_obj: Dynamic): ctp.Theme = new {
  name = _obj.name
  order = _obj.order
  dark = _obj.dark
  colors = _obj.colors
            .toMap()
            .mapValues((_, v) -> dynToPaletteColor(v))
            .toTyped(ctp.Palette) // Note that this may cause bad behaviour
                              // See: https://pkl-lang.org/package-docs/pkl/current/base/Map.html#toTyped()
}

function fromJson(_json: String): ctp.Catppuccin =
  let (file = read(_json))
  let (parsed = new json.Parser {}.parse(file))
  new {
    mocha = dynToTheme(parsed.mocha)
    macchiato = dynToTheme(parsed.macchiato)
    frappe = dynToTheme(parsed.frappe)
    latte = dynToTheme(parsed.latte)
  }